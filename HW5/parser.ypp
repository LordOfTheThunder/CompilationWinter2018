%{

#include <iostream>
#include <cstdlib>
#include <cassert>
#include <string>
using namespace std;

#include "output.hpp"
#include "symbolTable.hpp"
using namespace output;
#include "StackStructs.h"
#include "hw5.hpp"

int yylex();
void yyerror(const char *);
void CheckCorrectTypes(string _type1, string id2, string _type2);

%}

%token VOID
%token INT
%token BYTE
%token B
%token BOOL
%token STRUCT
%left AND
%left OR
%right NOT
%token TRUE
%token FALSE
%token RETURN
%left IF
%left ELSE
%token WHILE
%token BREAK
%token CONTINUE
%left SC
%token COMMA
%token PERIOD
%token LPAREN
%token RPAREN
%token LBRACE
%token RBRACE
%left ASSIGN
%nonassoc RELOP
%left BINOPAS
%left BINOPMD
%token ID
%token NUM
%token STRING


%%
Program: Structs Funcs  {

}

Funcs: FuncDecl Funcs {
}
	|	/*epsilon*/ ;

FuncDecl: FuncDecl1 FuncDecl2 {
	tables->popScope();
    callReturnFunc();
}

FuncDecl1: RetType ID LPAREN Formals RPAREN {
	tables->addFunction(typeToString($1.type), $2.str, $4.func_info, $1.lineno);
	$$.type = $1.type;
	$$.lineno = $1.lineno;
	defFunction($2.str);
}

FuncDecl2: LBRACE Statements RBRACE {
	$$.lineno = $2.lineno;
	BPATCH($2.getNextList(), GEN_LABEL());
}

Structs: StructsDecl Structs {
}
	| /*epsilon*/ {

	}

StructsDecl: STRUCT ID LBRACE StructMemList RBRACE SC {
		$$.str = string($2.str);
		tables->addStruct($$.str, $4.func_info, $1.lineno);
		$$.lineno = $1.lineno;
	}

RetType: Type {
	$$.type = $1.type;
}
	| VOID {
		$$.type = types_Void;
	}

Formals: FormalsList {
	tables->setLine($1.lineno);
	$$.func_info = $1.func_info;
}
	| /*epsilon*/ {
		$$.func_info.clear();
	}

FormalsList: FormalDecl {
	if ($1.struct_type != "") {
		// we have a struct type
		$$.func_info.insert($$.func_info.begin(),varPair($1.struct_type, $1.str, $1.lineno, $1.reg));
	}
	else {
		$$.func_info.insert($$.func_info.begin(),varPair(typeToString($1.type), $1.str, $1.lineno, $1.reg));
	}
	tables->setLine($1.lineno);
	$$.lineno = $1.lineno;
}
	| FormalDecl COMMA FormalsList {
		$$.func_info = $3.func_info;
		if ($1.struct_type != "") {
			// we have a struct type
			$$.func_info.insert($$.func_info.begin(),varPair($1.struct_type, $1.str, $1.lineno, $1.reg));
		}
		else {
			$$.func_info.insert($$.func_info.begin(),varPair(typeToString($1.type), $1.str, $1.lineno, $1.reg));
		}
		tables->setLine($1.lineno);
		$$.lineno = $1.lineno;
	}

FormalDecl: Type ID {
	tables->setLine($2.lineno);
	$$.lineno = $2.lineno;
	$$.type = $1.type;
	$$.str = $2.str;
}
	| StructType ID {
		tables->setLine($2.lineno);
		if (tables->getStruct($1.str) == NULL) {
			errorUndefStruct(tables->getLine(), $1.str);
			exit(0);
		}
		$$.str = $2.str;
		$$.struct_type = $1.str;
		$$.lineno = $2.lineno;
	}


StructMemList: StructMem {
	$$.func_info.insert($$.func_info.begin(),varPair(typeToString($1.type), $1.str, $1.lineno, $1.reg));
}
	| StructMem StructMemList {
		$$.func_info = $2.func_info;
		$$.func_info.insert($$.func_info.begin(),varPair(typeToString($1.type), $1.str, $1.lineno, $1.reg));
	}

StructMem: Type ID SC {
	tables->setLine($2.lineno);
	$$.lineno = $2.lineno;
	$$.type = $1.type;
	$$.str = $2.str;
}

Statements: Statement {
	$$.lineno = $1.lineno;
	tables->setLine($$.lineno);

	$$ = $1;
    }
	| Statements MMarker Statement {
		$$.lineno = $3.lineno;
		tables->setLine($$.lineno);

		$$ = $3;
		BPATCH($1.getNextList(), $2.str);
		$$.getBreakList() = MERGE($1.getBreakList(), $3.getBreakList());
		$$.getContinueList() = MERGE($1.getContinueList(), $3.getContinueList());

	}

StartScope: /*epsilon*/ {
	tables->addScope();
}

StartWhileScope: /*epsilon*/ {
	tables->addWhile();
}

Statement: StartScope LBRACE Statements RBRACE {
	tables->popScope();
	$$ = $3;
	$$.lineno = $2.lineno;

}

	| Type ID SC {
	tables->addVariable(typeToString($1.type), $2.str, $1.lineno);
	$$.lineno = $1.lineno;
	allocateVar();
}

	| StructType ID SC {
		tables->addVariable($1.str, $2.str, $1.lineno);
		$$.lineno = $1.lineno;
		//allocateStruct(tables->getStruct($1.str));
		allocateStruct(tables->getStruct($1.str), $2.str);
	}

	| STRUCT ID LBRACE StructMemList RBRACE SC {
		$$.str = string($2.str);
		tables->addStruct($$.str, $4.func_info, $1.lineno);
		$$.lineno = $1.lineno;
	}

	| Type ID ASSIGN Exp SC {
		tables->setLine($1.lineno);
		if ($4.type == types_Undefined) {
			// We have a struct type
			CheckCorrectTypes(typeToString($1.type), $4.str, $4.struct_type);
		} else {
			CheckCorrectTypes(typeToString($1.type), $4.str, typeToString($4.type));
		}
		tables->addVariable(typeToString($1.type), $2.str, $4.lineno);
		if ($1.type != types_Bool) {
			allocateVar($4);
			$$.str = GEN_LABEL();
			BPATCH($4.getNextList(), $$.str);
			BPATCH($4.getTrueList(), $$.str);
			BPATCH($4.getFalseList(), $$.str);
		} else {
			string true_label = GEN_LABEL();
			$$.reg = reg_alloc->allocateRegister();
			stringstream s;
			s << "li " << register_type_to_str($$.reg) << ", 1";
			emit(s.str());
			emit("subu $sp, $sp, 4");
			s.clear();
			s.str(string());
			s << "sw " << register_type_to_str($$.reg) << ", ($sp)";
			emit(s.str());
			$$.getNextList() = MAKE_LIST(emit("j "));
			string false_label = GEN_LABEL();
			s.clear();
			s.str(string());
			s << "li " << register_type_to_str($$.reg) << ", 0";
			emit(s.str());
			emit("subu $sp, $sp, 4");
			s.clear();
			s.str(string());
			s << "sw " << register_type_to_str($$.reg) << ", ($sp)";
			emit(s.str());
			$$.getNextList() = MERGE($$.getNextList(), MAKE_LIST(emit("j ")));

			//BPATCH($$.getNextList(), false_label);
			BPATCH($4.getTrueList(), true_label);
			BPATCH($4.getFalseList(), false_label);
			reg_alloc->freeRegister($$.reg);
		}
	}

	| StructType ID ASSIGN Exp SC {
		if ($4.type == types_Undefined) {
			// We have a struct type
			CheckCorrectTypes($1.str, $4.str, $4.struct_type);
		} else {
			CheckCorrectTypes($1.str, $4.str, typeToString($4.type));
		}
		tables->addVariable($1.str, $2.str, $1.lineno);
		//allocateStruct(tables->getStruct($1.str), true);
		allocateStruct(tables->getStruct($1.str), $2.str, true);
	}

	| ID ASSIGN Exp SC {
		tables->setLine($$.lineno);
		// Check ID exists
		if (!tables->getVariable($1.str)) {
			errorUndef(tables->getLine(), $1.str);
			exit(0);
		}
		// Check correct type
		if ($3.type == types_Undefined) {
			// We have a struct type
			CheckCorrectTypes((tables->getVariable($1.str))->getType(), $3.str, $3.struct_type);
		} else {
			CheckCorrectTypes((tables->getVariable($1.str))->getType(), $3.str, typeToString($3.type));
		}
		StructEntry* st_entry = tables->getStruct((tables->getVariable($1.str))->getType());
		if (st_entry != NULL) {
			// We copy structs
			assignToStruct($1.str);
			$$.str = GEN_LABEL();
			BPATCH($3.getNextList(), $$.str);
			BPATCH($3.getTrueList(), $$.str);
			BPATCH($3.getFalseList(), $$.str);
		} else {
			VariableEntry* var_entry = tables->getVariable($1.str);
			assert(var_entry != NULL);
			if (var_entry->getType() != typeToString(types_Bool)) {
				assignToVar((tables->getVariable($1.str))->getWordOffset(), $3);
				$$.str = GEN_LABEL();
				BPATCH($3.getTrueList(), $$.str);
				BPATCH($3.getFalseList(), $$.str);
			} else {
				string true_label = GEN_LABEL();
				$$.reg = reg_alloc->allocateRegister();
				stringstream s;
				s << "li " << register_type_to_str($$.reg) << ", 1";
				emit(s.str());
				s.clear();
				s.str(string());
				int offset = var_entry->getWordOffset();
				s << "sw " << register_type_to_str($$.reg) << ", " << -offset << "($fp)";
				emit(s.str());
				$$.getNextList() = MAKE_LIST(emit("j "));
				string false_label = GEN_LABEL();
				s.clear();
				s.str(string());
				s << "li " << register_type_to_str($$.reg) << ", 0";
				emit(s.str());
				s.clear();
				s.str(string());
				s << "sw " << register_type_to_str($$.reg) << ", " << -offset << "($fp)";
				emit(s.str());
				$$.getNextList() = MERGE($$.getNextList(), MAKE_LIST(emit("j ")));

				BPATCH($3.getTrueList(), true_label);
				BPATCH($3.getFalseList(), false_label);
				reg_alloc->freeRegister($$.reg);
			}
		}



	}

	| ID PERIOD ID ASSIGN Exp SC {
		$$.lineno = $1.lineno;
		tables->setLine($$.lineno);
		// Check if variable of struct type exists
		VariableEntry* var_entry = tables->getVariable($1.str);
		if (var_entry == NULL) {
			errorUndef(tables->getLine(), $1.str);
			exit(0);
		}
		// Check if struct itself exists
		StructEntry* struct_entry = tables->getStruct((tables->getVariable($1.str))->getType());
		if (struct_entry == NULL) {
			errorUndefStruct(tables->getLine(), $1.str);
			exit(0);
		}
		// Check if field of struct exists
		if (!struct_entry->varExists($3.str)) {
			errorUndefStructMember(tables->getLine(), $1.str);
			exit(0);
		}
		// Check type mismatch
		if ($5.type == types_Undefined) {
			// We have a struct type cast into field type...
			errorMismatch($$.lineno);
			exit(0);
		}
		if (!typesEqual(struct_entry->getTypeOfVar($3.str), typeToString($5.type))) {
			errorMismatch(tables->getLine());
			exit(0);
		}

		StructEntry* str_entry = tables->getStruct(var_entry->getType());
		assert(var_entry != NULL);
		if (str_entry->getTypeOfVar($3.str) != typeToString(types_Bool)) {
			assignToVar(tables->getStructMemberWordOffset($1.str, $3.str), $5);
			$$.str = GEN_LABEL();
			BPATCH($5.getTrueList(), $$.str);
			BPATCH($5.getFalseList(), $$.str);
		} else {
			string true_label = GEN_LABEL();
			$$.reg = reg_alloc->allocateRegister();
			stringstream s;
			s << "li " << register_type_to_str($$.reg) << ", 1";
			emit(s.str());
			s.clear();
			s.str(string());
			int offset = tables->getStructMemberWordOffset($1.str, $3.str);
			s << "sw " << register_type_to_str($$.reg) << ", " << -offset << "($fp)";
			emit(s.str());
			$$.getNextList() = MAKE_LIST(emit("j "));
			string false_label = GEN_LABEL();
			s.clear();
			s.str(string());
			s << "li " << register_type_to_str($$.reg) << ", 0";
			emit(s.str());
			s.clear();
			s.str(string());
			s << "sw " << register_type_to_str($$.reg) << ", " << -offset << "($fp)";
			emit(s.str());
			$$.getNextList() = MERGE($$.getNextList(), MAKE_LIST(emit("j ")));

			BPATCH($5.getTrueList(), true_label);
			BPATCH($5.getFalseList(), false_label);
			reg_alloc->freeRegister($$.reg);
		}
	}

	| Call SC {
		$$.lineno = $2.lineno;
		tables->setLine($$.lineno);
		reg_alloc->freeRegister($1.reg);
	}

	| RETURN SC {
		$$.lineno = $1.lineno;
		tables->setLine($$.lineno);
		tables->checkReturn(typeToString(types_Void), $$.lineno);
		callReturnFunc();
	}

	| RETURN Exp SC {
		$$.lineno = $1.lineno;
		tables->setLine($$.lineno);
		tables->checkReturn(typeToString($2.type), $$.lineno);
		if (typeToString($2.type) == typeToString(types_Bool)){
			string true_label = GEN_LABEL();
			emit("li $v0, 1");
			emit("move $sp, $fp");
			// register_type reg = reg_alloc->allocateRegister();
			// emit("subu $sp, $sp, 4");
			// emit("li " + register_type_to_str(reg) + ", 1");
			// emit("sw " + register_type_to_str(reg) + ", ($sp)");
    		emit("jr $ra");
			string false_label = GEN_LABEL();
			// emit("subu $sp, $sp, 4");
			// emit("li " + register_type_to_str(reg) + ", 0");
			// emit("sw " + register_type_to_str(reg) + ", ($sp)");
			emit("li $v0, 0");
			emit("move $sp, $fp");
    		emit("jr $ra");
			//reg_alloc->freeRegister(reg);
			BPATCH($2.getTrueList(), true_label);
			BPATCH($2.getFalseList(), false_label);
		} else {
			returnValueFromFunction($2);
			callReturnFunc();
			$$.str = GEN_LABEL();
			BPATCH($2.getTrueList(), $$.str);
			BPATCH($2.getFalseList(), $$.str);
		}
	}

	| If_Scope %prec IF {
        $$.getBreakList() = $1.getBreakList();
        $$.getNextList() = MERGE($1.getNextList(), $1.getFalseList());
        $$.getContinueList() = $1.getContinueList();
	}

	| If_Scope ElseScope {
        BPATCH($1.getFalseList(), $2.str);
        $$.getNextList() = MERGE($1.getNextList(), $2.getNextList());
        $$.getBreakList() = MERGE($1.getBreakList(), $2.getBreakList());
        $$.getContinueList() = MERGE($1.getContinueList(), $2.getContinueList());
	}
    //      1           2     3       4     5    6      7         8        9
	| StartWhileScope WHILE LPAREN MMarker Exp RPAREN MMarker Statement NMarker{
		tables->popScope();
		if ($5.type != types_Bool) {
			errorMismatch($2.lineno);
			exit(0);
		}

		BPATCH($8.getContinueList(), $4.str);
		BPATCH($8.getNextList(), $4.str);
		BPATCH($9.getNextList(), $4.str);
		BPATCH($5.getTrueList(), $7.str);
		$$.getNextList() = MERGE($5.getFalseList(), $8.getBreakList());

		reg_alloc->freeRegister($5.reg);
	}

	| BREAK SC {
		tables->isBreakAllowed($1.lineno);
		$$.getBreakList() = MAKE_LIST(emit("j "));
	}

	| CONTINUE SC {
		tables->isContinueAllowed($1.lineno);
		$$.getContinueList() = MAKE_LIST(emit("j "));
	}

If_Scope: StartScope IF LPAREN Exp RPAREN MMarker Statement {
	tables->popScope();
	$$.lineno = $2.lineno;
	tables->setLine($2.lineno);
	if ($4.type != types_Bool) {
		errorMismatch($2.lineno);
		exit(0);
	}

    BPATCH($4.getTrueList(), $6.str);
    $$.getNextList() = $7.getNextList();
    $$.getFalseList() = $4.getFalseList();
    $$.getBreakList() = $7.getBreakList();
    $$.getContinueList() = $7.getContinueList();

	reg_alloc->freeRegister($4.reg);
}

ElseScope: ELSE NMarker MMarker StartScope Statement {
	tables->popScope();
	$$.lineno = $5.lineno;
	tables->setLine($$.lineno);
	$$.getBreakList() = $5.getBreakList();
	$$.getContinueList() = $5.getContinueList();
	$$.getNextList() = MERGE($2.getNextList(), $5.getNextList());
	$$.str = $3.str; // beginning label of the else scope
}

Call: ID LPAREN ExpList RPAREN {
	$$.str = $1.str;
	tables->callFunction($1.str, $3.func_info, $1.lineno);
	BPATCH($3.getNextList(), GEN_LABEL());
	$$.reg = callFunction($1.str, $3);
}
	| ID LPAREN RPAREN {
		$$.str = $1.str;
		vector<varPair> t;
		tables->callFunction($1.str, t, $1.lineno);
		$$.reg = callFunction($1.str);
	}

ExpList: Arg {
    /*
	if (typeToString($1.type) == typeToString(types_Bool)) {
		string true_label = GEN_LABEL();
		$$.reg = reg_alloc->allocateRegister();
		stringstream s;
		s << "li " << register_type_to_str($$.reg) << ", 1";
		emit(s.str());
		$$.getNextList() = MAKE_LIST(emit("j "));
		string false_label = GEN_LABEL();
		s.clear();
		s.str(string());
		s << "li " << register_type_to_str($$.reg) << ", 0";
		emit(s.str());
		$$.getNextList() = MERGE($$.getNextList(), MAKE_LIST(emit("j ")));

		BPATCH($1.getTrueList(), true_label);
		BPATCH($1.getFalseList(), false_label);
		$1.reg = $$.reg;
	}
	*/

	if ($1.type == types_Undefined) {
		$$.func_info.insert($$.func_info.begin(), varPair($1.struct_type, $1.str, $1.lineno, $1.reg));
	} else {
		$$.func_info.insert($$.func_info.begin(), varPair(typeToString($1.type), $1.str, $1.lineno, $1.reg));
	}
	$$.type = $1.type;
	$$.str = $1.str;
}
	| Arg COMMA ExpList {
		$$.func_info = $3.func_info;
		$$.type = $1.type;
        /*
		if (typeToString($1.type) == typeToString(types_Bool)) {
			string true_label = GEN_LABEL();
			$$.reg = reg_alloc->allocateRegister();
			stringstream s;
			s << "li " << register_type_to_str($$.reg) << ", 1";
			emit(s.str());
			$$.getNextList() = MERGE($3.getNextList(), MAKE_LIST(emit("j ")));
			string false_label = GEN_LABEL();
			s.clear();
			s.str(string());
			s << "li " << register_type_to_str($$.reg) << ", 0";
			emit(s.str());
			$$.getNextList() = MERGE($$.getNextList(), MAKE_LIST(emit("j ")));

			BPATCH($1.getTrueList(), true_label);
			BPATCH($1.getFalseList(), false_label);

			$1.reg = $$.reg;
		}
        */
		if ($1.type == types_Undefined) {
			$$.func_info.insert($$.func_info.begin(), varPair($1.struct_type, $1.str, $1.lineno, $1.reg));
		} else {
			$$.func_info.insert($$.func_info.begin(), varPair(typeToString($1.type), $1.str, $1.lineno, $1.reg));
		}
	}

Arg: Exp {
    if(typeToString($1.type) == typeToString(types_Bool)){
        $$.reg = reg_alloc->allocateRegister();

        // store 1 in temp register
        BPATCH($1.getTrueList(), GEN_LABEL());
        emit("li " + register_type_to_str($$.reg) + ", 1");
        vector<int> bypass_false = MAKE_LIST(emit("j "));
        BPATCH($1.getFalseList(), GEN_LABEL());
        emit("li " + register_type_to_str($$.reg) + ", 0");
        BPATCH(bypass_false, GEN_LABEL());
    } else {
        $$ = $1;
    }
}

Type: INT {
	$$.type = types_Int;
}

	| BYTE {
		$$.type = types_Byte;
	}

	| BOOL {
		$$.type = types_Bool;
	}

StructType: STRUCT ID {
	$$.str = $2.str;
}

Exp: LPAREN Exp RPAREN {
    $$ = $2;

}   | Exp BINOPAS Exp {
	if (($1.type != types_Int && $1.type != types_Byte) || ($3.type != types_Int && $3.type != types_Byte)) {
		// Both must be numbers
		errorMismatch($2.lineno);
		exit(0);
	}
	$$.lineno = $1.lineno;
	if ($1.type == types_Int || $3.type == types_Int) {
		$$.type = types_Int;
	} else {
		$$.type = types_Byte;
	}
	$$.reg = arithmetic_op($1, $3, $2.op);

}	| Exp BINOPMD Exp {
		if (($1.type != types_Int && $1.type != types_Byte) || ($3.type != types_Int && $3.type != types_Byte)) {
			// Both must be numbers
			errorMismatch($2.lineno);
			exit(0);
		}
		if ($1.type == types_Int || $3.type == types_Int) {
			$$.type = types_Int;
		} else {
			$$.type = types_Byte;
		}
		$$.lineno = $1.lineno;
		if ($2.op == div_op) {
			checkDivisionByZero($3);
		}
		$$.reg = arithmetic_op($1, $3, $2.op);
	}

	| ID {
		$$.str = $1.str;
		$$.lineno = $1.lineno;
		// Check if function
		if (tables->getFunction($1.str) != NULL) {
			$$.type = stringToType((tables->getFunction($1.str))->getType());
		}
		// Check if variable
		else if (tables->getVariable($1.str) != NULL) {
			$$.type = stringToType((tables->getVariable($1.str))->getType());
			// Allocate register
			// no allocation if we copy structs
			if (tables->getStruct((tables->getVariable($1.str))->getType()) == NULL) {
				$$.reg = loadToRegister(tables->getVariable($1.str));
				VariableEntry* var_entry = tables->getVariable($1.str);
				if (stringToType(var_entry->getType()) == types_Bool){
					// load data to reg   1 is true, 0 is false
					register_type tmp_reg = loadImmediateToRegister("1");
					$$.getTrueList() = MAKE_LIST(emit("beq " + register_type_to_str($$.reg) + ", " + register_type_to_str(tmp_reg) + ", "));
					$$.getFalseList() = MAKE_LIST(emit("j "));
					reg_alloc->freeRegister($$.reg);
					reg_alloc->freeRegister(tmp_reg);
				}

			}
		}
		else {
			// Not defined ...
			errorUndef($$.lineno, $$.str);
			exit(0);
		}
		if ($$.type == types_Undefined) {
			// We probably have a struct type...
			VariableEntry* var = tables->getVariable($$.str);
			StructEntry* struct_type = tables->getStruct(var->getType());
			if (struct_type == NULL) {
				// WTF?
				errorUndefStruct($$.lineno, $$.str);
				exit(0);
			}
			$$.struct_type = struct_type->getId();
			// Allocate registers for struct
			// MAX TODO: fill this somehow later
		}
	}

	| ID PERIOD ID {
		$$.lineno = $1.lineno;
		tables->setLine($$.lineno);
		// Check if variable of struct type exists
		VariableEntry* var_entry = tables->getVariable($1.str);
		if (var_entry == NULL) {
			errorUndef(tables->getLine(), $1.str);
			exit(0);
		}
		// Check if struct itself exists
		StructEntry* struct_entry = tables->getStruct((tables->getVariable($1.str))->getType());
		if (struct_entry == NULL) {
			errorUndefStruct(tables->getLine(), $1.str);
			exit(0);
		}
		// Check if field of struct exists
		if (!struct_entry->varExists($3.str)) {
			errorUndefStructMember(tables->getLine(), $1.str);
			exit(0);
		}

		if (struct_entry->getTypeOfVar($3.str) == typeToString(types_Bool)){
			// load data to reg   1 is true, 0 is false
			$$.reg = loadStructMemberToRegister($1.str, $3.str);
			register_type tmp_reg = loadImmediateToRegister("1");
			$$.getTrueList() = MAKE_LIST(emit("beq " + register_type_to_str($$.reg) + ", " + register_type_to_str(tmp_reg) + ", "));
			$$.getFalseList() = MAKE_LIST(emit("j "));
			reg_alloc->freeRegister($$.reg);
			reg_alloc->freeRegister(tmp_reg);
		}

		$$.type = stringToType(struct_entry->getTypeOfVar($3.str));
		$$.str = string($1.str + "." + $3.str);
		// TODO: Allocate register
		$$.reg = loadStructMemberToRegister($1.str, $3.str);
	}

	| Call {
		$$.str = $1.str;
		$$.lineno = $1.lineno;
		FunctionEntry* func_entry = tables->getFunction($$.str);
		if (func_entry == NULL) {
			errorUndefFunc($$.lineno, $$.str);
			exit(0);
		}
		$$.type = stringToType(func_entry->getType());
		$$.reg = $1.reg;

		if (func_entry->getType() == typeToString(types_Bool)){
			register_type tmp_reg = loadImmediateToRegister("1");
			$$.getTrueList() = MAKE_LIST(emit("beq " + register_type_to_str($1.reg) + ", " + register_type_to_str(tmp_reg) + ", "));
			$$.getFalseList() = MAKE_LIST(emit("j "));
			reg_alloc->freeRegister(tmp_reg);
		}
	}

	| NUM {
		$$.str = $1.str;
		tables->setLine($$.lineno);
		$$.type = types_Int;
		$$.lineno = $1.lineno;
	}

	| NUM B {
		$$.str = $1.str;
		int num = atoi($$.str.c_str());
		tables->setLine($$.lineno);
		if (num < 0 || num > 255) {
			errorByteTooLarge($$.lineno, $$.str);
			exit(0);
		}
		$$.type = types_Byte;
		$$.lineno = $1.lineno;
	}

	| STRING {
		$$.str = $1.str;
		$$.type = types_String;
		$$.lineno = $1.lineno;
		tables->setLine($$.lineno);
	}

	| TRUE {
		$$.str = $1.str;
		$$.type = types_Bool;
		$$.lineno = $1.lineno;

        $$.getTrueList() = MAKE_LIST(emit("j "));
        $$.reg = no_reg;
	}

	| FALSE {
		$$.str = $1.str;
		$$.type = types_Bool;
		$$.lineno = $1.lineno;

		$$.getFalseList() = MAKE_LIST(emit("j "));
        $$.reg = no_reg;
	}

	| NOT Exp {
		if ($2.type != types_Bool) {
			// Not can only be used on booleans
			errorMismatch($2.lineno);
			exit(0);
		}
		$$.type = types_Bool;
		$$.lineno = $2.lineno;

		$$.getTrueList() = $2.getFalseList();
		$$.getFalseList() = $2.getTrueList();
        $$.reg = no_reg;
	}

	| Exp AND MMarker Exp {
		if ($1.type != types_Bool) {
			// And can only be used on booleans
			errorMismatch($1.lineno);
			exit(0);
		}
		if ($4.type != types_Bool) {
			// And can only be used on booleans
			errorMismatch($4.lineno);
			exit(0);
		}
		$$.lineno = $1.lineno;
		$$.type = types_Bool;

		BPATCH($1.getTrueList(), $3.str);
		$$.getFalseList() = MERGE($1.getFalseList(), $4.getFalseList());
		$$.getTrueList() = $4.getTrueList();
		$$.reg = no_reg;

		reg_alloc->freeRegister($1.reg);
		reg_alloc->freeRegister($4.reg);
	}

	| Exp OR MMarker Exp {
		if ($1.type != types_Bool) {
			// And can only be used on booleans
			errorMismatch($1.lineno);
			exit(0);
		}
		if ($4.type != types_Bool) {
			// And can only be used on booleans
			errorMismatch($4.lineno);
			exit(0);
		}
		$$.lineno = $1.lineno;
		$$.type = types_Bool;

		BPATCH($1.getFalseList(), $3.str);
        $$.getTrueList() = MERGE($1.getTrueList(), $4.getTrueList());
        $$.getFalseList() = $4.getFalseList();
        $$.reg = no_reg;

		reg_alloc->freeRegister($1.reg);
		reg_alloc->freeRegister($4.reg);
	}

	| Exp RELOP Exp {
		if (($1.type != types_Int && $1.type != types_Byte)) {
			// Both must be numbers
			errorMismatch($1.lineno);
			exit(0);
		}
		if ($3.type != types_Int && $3.type != types_Byte) {
			// Both must be numbers
			errorMismatch($3.lineno);
			exit(0);
		}
		$$.type = types_Bool;
		$$.lineno = $1.lineno;

		int r1 = 0;
		int r2 = 0;
		if ($1.reg == no_reg) {
            $1.reg = loadImmediateToRegister($1.str);
            r1 = 1;
        }
        if ($3.reg == no_reg) {
            $3.reg = loadImmediateToRegister($3.str);
            r2 = 1;
        }

        logicalRelops($2.str, $1.reg, $3.reg, $$.getTrueList(), $$.getFalseList());
		reg_alloc->freeRegister($1.reg);
		reg_alloc->freeRegister($3.reg);
        $$.reg = no_reg;
	}

MMarker: {
	$$ = StackType();
	$$.str = GEN_LABEL();
}

NMarker: {
	$$ = StackType();
	$$.getNextList() = MAKE_LIST(emit("j "));
}


%%

int main() {
	tables = new symbolTable();
	reg_alloc = new RegisterAllocation();
	init_text();
	yyparse();
	tables->existsMain();
	delete tables;
	delete reg_alloc;
	print_data_buffer;
	print_code_buffer;
	return 0;
}


void yyerror(const char *) {
	output::errorSyn(tables->getLine());
	exit(0);
}

void CheckCorrectTypes(string type1, string id2, string type2) {
		if (tables->getFunction(id2) == NULL && tables->getVariable(id2) == NULL && type2 == typeToString(types_Undefined)) {
			// The parameter is neither a function or a variable. ERROR!
			errorUndef(tables->getLine(), id2);
			exit(0);
		}

		if (tables->getVariable(id2) != NULL && type2 == "undefined") {
			// Try to get the type from the table
			type2 = (tables->getVariable(id2))->getType();
		}
		if (tables->getFunction(id2) != NULL) {
			// This is a function. Treat it as a function
			type2 = (tables->getFunction(id2))->getType();
		}
		if (!typesEqual(type1, type2)) {
			errorMismatch(tables->getLine());
			exit(0);
		}
}